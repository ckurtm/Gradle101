apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.peirr.examples.flavor_dimens"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "abi", "version"

    productFlavors {

        amazon {
            dimension "version"
            applicationId "com.peirr.test.amazon"
        }

        google {
            buildConfigField "String", "CONTENT_AUTHORITY", "\"com.peirr.workout.play\""
            resValue "string", "authority", "com.peirr.workout.play"
            dimension "version"
            applicationId "com.peirr.test.play"
        }

        x86 {
            dimension "abi"
            versionCode = 4
        }

        arm {
            dimension "abi"
            versionCode = 3
        }
    }

    applicationVariants.all { variant ->
        def abiVersion = variant.productFlavors.get(0).versionCode
        variant.mergedFlavor.versionCode = 160000000 + abiVersion * 1000000 + defaultConfig.versionCode
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.0'
}
